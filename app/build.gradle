buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.travlog.android.apps"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true

        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "GIT_SHA", "\"${commitSha()}\""
        buildConfigField "String", "BUILD_DATE", "\"${commitTime()}\""
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            if (System.getenv("TRAVIS")) {
                if (System.getenv("TRAVIS_BRANCH") == "master") {
                    storeFile rootProject.file('travlog.jks')
                    storePassword System.getenv("KEYSTORE_PASS")
                    keyAlias System.getenv("ALIAS_NAME")
                    keyPassword System.getenv("ALIAS_PASS")
                }
            } else {
//                Properties properties = new Properties()
//                properties.load(rootProject.file('release.properties').newDataInputStream())
//
//                storeFile rootProject.file('travlog.jks')
//                storePassword properties['KEYSTORE_PASS']
//                keyAlias properties['ALIAS_NAME']
//                keyPassword properties['ALIAS_PASS']
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
        }
        staging {
            applicationIdSuffix ".staging"
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug

//            if (System.getenv("TRAVIS")) {
                ext.betaDistributionEmailsFilePath = "beta_distribution_emails.txt"
                ext.betaDistributionReleaseNotesFilePath = "release_notes.txt"
//            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName = "travlog-${output.name}"

            if (output.zipAlign) {
                outputFileName = newName + ".apk"
            } else {
                output.packageApplication.outputFile = newName + "-unaligned.apk"
            }
        }
    }
}

task debug() {
    dependsOn('clean', 'assembleDebug')
}

task staging() {
    dependsOn('clean', 'assembleStaging')
}

task release() {
    dependsOn('clean', 'assembleRelease')
}


debug.mustRunAfter clean
staging.mustRunAfter clean
release.mustRunAfter clean

dependencies {
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'com.google.android.material:material:1.1.0-alpha06'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin"

    implementation 'com.facebook.stetho:stetho:1.5.0'

    implementation "com.google.android.gms:play-services-auth:$firebase"

    implementation "com.google.dagger:dagger:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"

    implementation 'com.jakewharton.timber:timber:4.7.0'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'

    implementation "com.trello.rxlifecycle3:rxlifecycle-kotlin:$rxlifecycle"
    implementation "com.trello.rxlifecycle3:rxlifecycle-android-lifecycle-kotlin:$rxlifecycle"
    implementation "com.trello.rxlifecycle3:rxlifecycle-components:$rxlifecycle"

    implementation "com.jakewharton.rxbinding3:rxbinding-core:$rxbinding"
    implementation "com.jakewharton.rxbinding3:rxbinding-viewpager:$rxbinding"

    implementation "com.jakewharton:butterknife:$butterknife"
    kapt "com.jakewharton:butterknife-compiler:$butterknife"

    implementation 'com.facebook.android:facebook-login:4.29.0'

    implementation 'joda-time:joda-time:2.9.9'

    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    implementation "com.github.bumptech.glide:glide:$glide"
    kapt "com.github.bumptech.glide:compiler:$glide"
    implementation 'jp.wasabeef:glide-transformations:4.0.0'

    implementation 'com.github.arimorty:floatingsearchview:2.1.1'
    implementation 'com.wdullaer:materialdatetimepicker:3.5.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true;
    }
    implementation 'com.savvi.datepicker:rangepicker:1.2.0'
}
apply plugin: 'com.google.gms.google-services'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.3'
        languageVersion = '1.3'
    }
}

/*
 * Copyright 2019 Travlog. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// SHA and timestamp caching courtesy of https://github.com/gdg-x/frisbee/blob/develop/app/build.gradle#L193-L218
def commitSha() {
    def file = new File(project.buildDir, "commit-sha.txt")
    if (file.exists()) {
        return file.text
    }

    def value = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

    file.parentFile.mkdirs()
    file.text = value
    return value
}

def commitTime() {
    def file = new File(project.buildDir, "commit-timestamp.txt")
    if (file.exists()) {
        return file.text
    }

    def date = new Date()
    def value = date.format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

    file.parentFile.mkdirs()
    file.text = value
    return value
}